# jaf prompt theme
# copied from wunjo theme and modified
# time since last commit code from Gary Bernhardt
#   http://bitbucket.org/garybernhardt/dotfiles/src/tip/.zsh/func/prompt_jaf_setup

autoload -U zgitinit
zgitinit

autoload -U spectrum
spectrum

prompt_jaf_help () {
  cat <<'EOF'

  prompt jaf

EOF
}

revstring() {
  git describe --always $1 2>/dev/null ||
  git rev-parse --short $1 2>/dev/null
}

function prompt_jaf_scm_info() {
    if zgit_inworktree; then
      echo "($(prompt_wunjo_scm_branch)$(prompt_jaf_active_jobs))"
    fi
}

function prompt_jaf_active_jobs() {
  local num_jobs
  num_jobs=$(jobs | wc -l | tr -s ' ' | sed -e 's/^[[:space:]]*//')

  local -A pc
  pc=(${(kv)wunjo_prompt_colors})

  [ "$num_jobs" -eq 0 ] && return

  if [ "$num_jobs" -gt 8 ]; then
    COLOR="$pc[active_jobs_high]"
  elif [ "$num_jobs" -gt 3 ]; then
    COLOR="$pc[active_jobs_medium]"
  else
    COLOR="$pc[active_jobs_low]"
  fi

  local ACTIVE_JOBS="${COLOR}$num_jobs$pc[reset]"
  echo "|$ACTIVE_JOBS"
}

function prompt_jaf_rvm_info() {
  local ruby_version
  local -A pc
  pc=(${(kv)wunjo_prompt_colors})
  ruby_version=$($HOME/bin/rvm-prompt g u 2>/dev/null) || return
  if [ ! $ruby_version = '' ]; then
    echo "$pc[rvm_info] $ruby_version $pc[reset]"
  fi
}

prompt_jaf_setup() {
  local verbose
  if [[ $TERM == screen* ]] && [ -n "$STY" ]; then
    verbose=
  else
    verbose=1
  fi

  typeset -A colorcode
  colorcode[brblack]=234
  colorcode[brgreen]=240
  colorcode[bryellow]=241
  colorcode[brblue]=244
  colorcode[brcyan]=245
  colorcode[brwhite]=230
  colorcode[brred]=166
  colorcode[brmagenta]=061
  colorcode[black]=235
  colorcode[red]=160
  colorcode[green]=064
  colorcode[yellow]=136
  colorcode[blue]=033
  colorcode[magenta]=125
  colorcode[cyan]=037
  colorcode[white]=254
  colorcode[default]=235

  local -A solarized solar
  solar=(brblack base03 black base02 brgreen base01 bryellow base00 brblue base0 brcyan base1 white base2 brwhite base3 yellow yellow brred orange red red magenta magenta brmagenta violet blue blue cyan cyan green green)

  for cn in ${(k)solar}; do
    scn=$solar[$cn]
    solarized[${scn}]=${colorcode[$cn]}
  done

  typeset -A attcode
  attcode[none]=00
  attcode[bold]=01
  attcode[faint]=02
  attcode[standout]=03
  attcode[underline]=04
  attcode[blink]=05
  attcode[reverse]=07
  attcode[conceal]=08
  attcode[normal]=22
  attcode[no-standout]=23
  attcode[no-underline]=24
  attcode[no-blink]=25
  attcode[no-reverse]=27
  attcode[no-conceal]=28

  local -A pc
  pc[default]='default'
  pc[date]='cyan'
  pc[time]='blue'
  pc[host]='green'
  pc[user]='cyan'
  pc[punc]='yellow'
  pc[line]='magenta'
  pc[hist]='green'
  pc[path]='cyan'
  pc[shortpath]='default'
  pc[rc]='red'
  pc[scm_branch]='green'
  pc[scm_commitid]='yellow'
  pc[scm_status_dirty]='red'
  pc[scm_status_staged]='violet'
  pc[active_jobs_low]='green'
  pc[active_jobs_medium]='yellow'
  pc[active_jobs_high]='red'
  pc[rvm_info]='base1'
  pc[#]='yellow'

  for cn in ${(k)pc}; do
    code="$solarized[$pc[$cn]]"
    pc[${cn}]=$FG[$code]
  done

  pc[reset]=$FX[reset]

  typeset -Ag wunjo_prompt_colors
  wunjo_prompt_colors=(${(kv)pc})

  PROMPT="\$(prompt_jaf_rvm_info)"
  PROMPT+="\$(prompt_jaf_scm_info)"
  PROMPT+=" $pc[#]%#$pc[reset] "

  cat -v <(echo ${PROMPT})

  export PROMPT
  precmd_functions+='prompt_wunjo_precmd'
}

prompt_wunjo_precmd() {
  local ex=$?
  psvar=()

  if [[ $ex -ge 128 ]]; then
    sig=$signals[$ex-127]
    psvar[1]="sig${(L)sig}"
  else
    psvar[1]="$ex"
  fi
}

prompt_wunjo_scm_branch() {
  zgit_isgit || return
  local -A pc
  pc=(${(kv)wunjo_prompt_colors})

  echo -n "$pc[punc]:$pc[scm_branch]$(zgit_head)"

  if zgit_inworktree; then
    if ! zgit_isindexclean; then
      echo -n "$pc[scm_status_staged]+"
    fi

    local -a dirty
    if ! zgit_isworktreeclean; then
      dirty+='!'
    fi

    if zgit_hasunmerged; then
      dirty+='*'
    fi

    if zgit_hasuntracked; then
      dirty+='?'
    fi

    if [ $#dirty -gt 0 ]; then
      echo -n "$pc[scm_status_dirty]${(j::)dirty}"
    fi
  fi

  echo $pc[reset]
}

prompt_jaf_setup "$@"

# vim:set ft=zsh:
